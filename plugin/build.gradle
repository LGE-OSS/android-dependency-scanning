// This project's rootProject is ":buildSrc", so it should have a copy of build-script settings of the "/build.gradle"
buildscript {
    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:3.4.0"
    }
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

group = 'org.fosslight'
archivesBaseName = 'android-dependency-scanning'
version = '1.0.0'

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.apache.commons:commons-lang3:3.4'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            pom.project {
                name 'Android Dependency Scanning'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Gradle plugin to analyze the android dependencies and generate the report.'
                url 'https://github.com/fosslight/android-dependency-scanning'

                scm {
                    url 'https://github.com/fosslight/android-dependency-scanning.git'
                }

                licenses {
                    license {
                        name 'Apache-2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'fosslight'
                        name 'fosslight'
                        email 'fosslight-dev@lge.com'
                    }
                }
            }
        }
    }
}

